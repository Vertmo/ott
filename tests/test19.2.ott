% cut-down version of POPLmark language with records and record patterns but without type variables, abstraction and application
% Based on test19.0.ott but with rules tweaked a little so that code_pred in Isabelle works
metavar x , termvar ::=
 {{ isa string }} {{ coq nat }} {{ hol string }}  {{ coq-equality }} {{ lex alphanum }}  
 {{ tex \mathit{[[termvar]]} }} {{ com  term variable  }} 
 {{ isavar ''[[termvar]]'' }} {{ holvar "[[termvar]]" }} {{ texvar \mathrm{[[termvar]]} }} 
 {{ ocamlvar "[[termvar]]" }}

metavar l, label, k ::=
 {{ isa string }} {{ coq nat }} {{ hol string }} {{ lex alphanum }}  {{ tex \mathit{[[label]]} }} 
 {{ com  field label  }}  {{ isavar ''[[label]]'' }} {{ holvar "[[label]]" }}
 {{ ocamlvar "[[label]]" }}

indexvar index, i, j, n, m  ::= {{ isa nat }} {{ coq nat }} {{ hol num }} {{ lex numeral }}
  {{ com indices }}

 grammar

T {{ hol Typ }}, S, U :: 'T_' ::=                                     {{ com type  }}
  | Top                              ::   :: Top                        {{ com maximum type }}   
  | T -> T'                          ::   :: Fun                        {{ com type of functions }}
  | { l1 : T1 , .. , ln : Tn }       ::   :: Rec                        {{ com record }}           
  | ( T )                            :: M :: paren                      {{ ich [[T]] }}
  | < T1 , .. , Tn >                 :: :: Tup 

t :: 't_' ::=                                                         {{ com  term  }}
  | x                                ::   :: Var                        {{ com variable }}         
  | \ x : T . t                      ::   :: Lam  (+ bind x in t +)     {{ com abstraction }}      
  | t t'                             ::   :: App                        {{ com application }}      
  | { l1 = t1 ,  .. , ln = tn }      ::   :: Rec                        {{ com record }}
  | t . l                            ::   :: Proj                       {{ com projection }} 
  | let p = t in t'                  ::   :: Let  (+ bind b(p) in t' +) {{ com pattern binding}}
  | ( t )                            :: M :: paren                      {{ ich [[t]] }} 
  | [ x |-> t ] t'                   :: M :: tsub                       {{ ich ( tsubst_t [[t]] [[x]] [[t']] ) }}
  | s t                              :: M :: tsubs                      {{ ich ( m_t_subst_t [[s]] [[t]] ) }}
  | < t1 , .. , tn >                 ::   :: Tup
  
p :: 'P_' ::=                                                         {{ com  pattern }}
  | x : T                            ::   :: Var  (+ b = x  +)          {{ com variable pattern }}
  | { l1 = p1 , .. , ln = pn }       ::   :: Rec  (+ b = b(p1 .. pn) +) {{ com  record pattern  }}

v :: 'v_' ::=                                                         {{ com  values  }}
  | \ x : T . t                      ::   :: Lam  (+ bind x in t +)     {{ com abstraction }}      
  | { l1 = v1 ,  .. , ln = vn }      ::   :: Rec                        {{ com record }}

G {{ tex \Gamma }}, D {{ tex \Delta }} :: 'G_' ::=                    {{ com type environment }}
  | empty                            ::   :: empty       
  | G , x : T                        ::   :: term
  | G1 , .. , Gn                     :: M :: dots                        {{ ich (flatten_G [[G1..Gn]]) }}  

s {{ tex \sigma }} :: 'S_' ::= {{ com multiple term substitution }} {{ isa (termvar*t) list }} {{ hol (termvar#t) list }} {{ coq list (termvar*t) }}
  | [ x |-> t ]                    ::   :: singleton {{ ih [ ([[x]],[[t]]) ] }} {{ coq (cons ([[x]],[[t]]) nil) }}
  | s1 , .. , sn                   ::   :: list   {{ isa List.concat [[s1..sn]] }} {{ hol (FLAT [[s1..sn]]) }} {{ coq (List.flat_map (fun x => x) [[s1..sn]]) }}

terminals :: terminals_ ::=
  |  \                     ::   :: lambda    {{ tex  \lambda }}
  |  ->                    ::   :: arrow     {{ tex  \rightarrow }}
  |  =>                    ::   :: Arrow     {{ tex  \Rightarrow }}
  | |-                     ::   :: turnstile {{ tex \vdash }}
  | -->                    ::   :: red       {{ tex \longrightarrow }}
  | Forall                 ::   :: forall    {{ tex \forall }}
  | <:                     ::   :: subtype   {{ tex <: }}
  | |->                    ::   :: mapsto    {{ tex \mapsto }}
  | /\                     ::   :: wedge     {{ tex \wedge }}
  | \/                     ::   :: vee       {{ tex \vee }}
  | =                      ::   :: eq        {{ tex \!\! = \!\! }}

formula :: formula_ ::=          
  | judgement              :: :: judgement
% | G = G'                 :: :: Geq     {{ ich [[G]] = [[G']] }}
  | x = x'                 :: :: xeq     {{ ich [[x]] = [[x']]  }}
  | ( formula )            :: :: paren   {{ ich ( [[formula]] ) }}
  | not formula            :: :: not     {{ isa Not( [[formula]] ) }}
                                         {{ coq not( [[formula]] ) }}
                                         {{ hol ~( [[formula]] ) }}
                                         {{ tex \neg [[ formula]] }}
%  | x isin dom ( G )      :: :: xin    {{ isa ? T. ([[x]],T,[[G]]):tin }} 
%                                         {{ tex [[x]] \in [[dom]]([[G]]) }}
  | forall i isin 1 -- m . formula :: :: forall 
                               {{ tex \forall [[i]] \in 1 .. [[m]] . [[formula]] }} 
                               {{ isa ![[i]] . ((1::nat)<=[[i]] & [[i]]<=[[m]]) ==> [[formula]] }} 
                               {{ hol ![[i]] . (1<=[[i]] /\ [[i]]<=[[m]]) ==> [[formula]] }} 
                               {{ coq (forall [[i]], (1<=[[i]] /\ [[i]] <= m) -> [[formula]]) }}

  | exists i isin 1 -- m . formula :: :: exists 
                               {{ tex \exists [[i]] \in 1 .. [[m]]. [[formula]] }}
                               {{ isa ?[[i]]. ((1::nat)<=[[i]] & i<=[[m]]) ==> [[formula]] }} 
                               {{ hol ?[[i]] . (1<=[[i]] /\ [[i]]<=[[m]]) ==> [[formula]] }} 
                               {{ coq exists [[i]], (1<=[[i]] /\ [[i]] <= [[m]]) -> [[formula]] }}

  | formula /\ formula'   :: :: and  {{ isa ([[formula]] & [[formula']]) }}
                                     {{ hol ([[formula]] /\ [[formula']]) }}
                                     {{ coq ([[formula]] /\ [[formula']]) }}

  | l = l'                :: :: leq  {{ ich ([[l]]=[[l']]) }}
 
% would be nice to write the above as  {{ isa ?[[T]]. [[X<:T isin G]] }}

%formulalist :: formulalist_ ::=          
   | formula1 ... formulan   :: :: dots


%subrules
%
%  v <:: t


substitutions
  single   t x :: tsubst    
  multiple t x :: m_t_subst 

freevars
  t x :: fv



 embed 
   {{ isa

lemma list_all_cong[fundef_cong]:
     "xs = ys ==> (!!x. x:set ys ==> P x = Q x) ==> list_all P xs = list_all Q ys"
  unfolding list_all_iff
  by auto



(* This is needed so monotonicity for inductive predicates using list_all get through *)
lemma [mono]:"
         (!! x. f x --> g x) ==> list_all (%b. b) (map f foo_list)-->
                    list_all (%b. b) (map g foo_list) "
		       apply(induct_tac foo_list, auto) done


 primrec append_G :: "G => G => G"
 where
 "append_G [[G]] [[empty]] = [[G]]"
| "append_G [[G]] [[G',x:T]] = (let [[G'']] = append_G [[G]] [[G']] in [[G'',x:T]])"

primrec flatten_G :: "G list => G"
where
 "flatten_G [] = [[empty]]"
| "flatten_G (Cons [[G]] Gs) = append_G [[G]] (flatten_G Gs)"
 }}
 
   {{ hol
 val _ = Define `
    (append_G [[G]] [[empty]] = [[G]])
 /\ (append_G [[G]] [[G',x:T]] = (let [[G'']] = append_G [[G]] [[G']] in [[G'',x:T]]))`;
 val _ = Define `
    (flatten_G NIL = [[empty]])
 /\ (flatten_G (CONS [[G]] Gs) = append_G [[G]] (flatten_G Gs))`;
 }}
 
 {{ coq 
 Fixpoint append_G (g1 g2 : G) {struct g2} : G :=
   match g2 with
   | G_empty => g1
   | G_term gh v t => G_term (append_G g1 gh) v t
 end.
 Fixpoint flatten_G (gl:list_G) : G :=
   match gl with 
   | Nil_list_G => G_empty
   | Cons_list_G g gs => append_G g (flatten_G gs)
 end.  }}

defns
Jtype :: '' ::=


defn
G |- t : T ::  :: Ty :: Ty_  {{ com term $[[t]]$ has type $[[T]]$ }} by 


G|-t1:T1 .. G|-tn:Tn
------------------------------------- :: Rcd
G|- {l1=t1,..,ln=tn}:{l1:T1,..,ln:Tn}


G |- x1:T1 .. G |- xn : Tn
------------------------------------------- :: Tup
G |- < x1 , .. , xn > : < T1 , .. , Tn >

%G |- x1:T1 .. G |- xn : Tn
%------------------------------------------- :: Tup2
%G |- < x1 , .. , xn, x , x'1 , .. , x'm > : < T1 , .. , Tn >


embed {{ isa

code_pred   (modes: i ⇒ i ⇒ o  ⇒  bool ) [show_steps,  show_mode_inference,  show_invalid_clauses] Ty .

}}
